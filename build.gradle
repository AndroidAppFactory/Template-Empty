// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: rootDir.toString() + '/config.gradle'

buildscript {
    repositories {
        maven { url "https://mirrors.tencent.com/nexus/repository/maven-public/" }
        maven { url "https://mirrors.tencent.com/repository/maven/thirdparty-snapshots/" }
        maven { url "https://repo1.maven.org/maven2" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.2'
        classpath "com.bihe0832.android:GradleDependenciesCheck:4.1.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.22"
    }
}

task clean(type: Delete) {
    println("clean " + rootProject.buildDir)
    delete rootProject.buildDir
}

allprojects {
    repositories {
        maven { url "https://mirrors.tencent.com/nexus/repository/maven-public/" }
        maven { url "https://mirrors.tencent.com/repository/maven/thirdparty-snapshots/" }
        maven { url "https://repo1.maven.org/maven2" }
        flatDir {
            dirs './../libs/'
        }
    }
}


subprojects {
    afterEvaluate { Project subproject ->
        // AGP 8.0+ 需要统一 Java 版本
        if (subproject.hasProperty('android')) {
            subproject.android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }
            }
        }
        subproject.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
            kotlinOptions {
                jvmTarget = "17"
            }
        }

        if (subproject.name.startsWith("Lib")) {
            subproject.getTasks().create("copyAAR", Copy.class) {
                group = "zixieDev"
                from 'build/outputs/aar/' + subproject.name + '-release.aar'
                from 'build/outputs/mapping/release/mapping.txt'
                into "./../libs/"
            }
            subproject.getTasks().getByName('copyAAR').dependsOn("assembleRelease")
        }
    }
}
