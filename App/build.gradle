apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
// 不同类型的包，仅仅修改下面两项即可，其余内容保持一致
project.ext.applicationID = "com.bihe0832.android.template.empty"
project.ext.applicationPrefix = "ZTEmpty"
project.ext.routerSchema = "ztempty"
project.ext.applicationName = "ZTEmptyDemo"

android {
    namespace project.ext.applicationID
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        applicationId applicationID
        minSdkVersion project.appMinSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionName project.versionName
        versionCode project.versionCode
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    packaging {
        jniLibs {
            useLegacyPackaging = true
            pickFirsts += project.ndk_abiFilters_all.split(",").collectMany { abiItem ->
                project.pickFirstList.collect { item -> "lib/${abiItem}/${item}" }
            }
        }
        resources {
            excludes += project.excludeList
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    lint {
        abortOnError false
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion project.compose_version
    }

    signingConfigs {
        debug {
            keyAlias project.signingConfigs.keyAlias
            keyPassword project.signingConfigs.keyPassword
            storeFile file(project.signingConfigs.storeFile)
            storePassword project.signingConfigs.storePassword
            v1SigningEnabled project.signingConfigs.v1SigningEnabled as boolean
            v2SigningEnabled project.signingConfigs.v2SigningEnabled as boolean
        }

        release {
            keyAlias project.signingConfigs.keyAlias
            keyPassword project.signingConfigs.keyPassword
            storeFile file(project.signingConfigs.storeFile)
            storePassword project.signingConfigs.storePassword
            v1SigningEnabled project.signingConfigs.v1SigningEnabled as boolean
            v2SigningEnabled project.signingConfigs.v2SigningEnabled as boolean
        }
    }


    buildTypes {
        debug {
            resValue "string", "app_name", applicationName
            resValue "string", "router_schema", routerSchema

            signingConfig signingConfigs.debug
            minifyEnabled false
            shrinkResources false
        }

        release {
            resValue "string", "app_name", applicationName
            resValue "string", "router_schema", routerSchema

            signingConfig signingConfigs.release
            minifyEnabled project.minifyEnabled as boolean
            proguardFiles "./../proguard-rules-common.pro"
            shrinkResources false
        }
    }
    android.applicationVariants.all { variant ->
        def finalFileName = applicationPrefix + "_V${defaultConfig.versionName}_${defaultConfig.versionCode}"
        variant.outputs.all {
            if (variant.buildType.name == 'debug') {
                outputFileName = finalFileName + "_debug.apk"
            } else {
                outputFileName = finalFileName + "_release.apk"
            }
        }
    }

    configurations.all {
        resolutionStrategy {
            force("androidx.lifecycle:lifecycle-livedata-core:2.6.0")
        }
    }
}

kapt {
    correctErrorTypes = true
}

dependencies {
    api "com.bihe0832.android:common-compose-debug:8.2.0"
    kapt "com.bihe0832.android:lib-router-compiler:5.1.7"
    api "com.bihe0832.android:common-wrapper-min:8.2.0"
}